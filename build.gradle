import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id "java"
    id "org.springframework.boot" version "4.0.2" apply false
    id "io.spring.dependency-management" version "1.1.7" apply false
}

allprojects {
    group = "com.fantasy"
    version = "0.0.1-SNAPSHOT"
    description = "FantasyMusic"

    repositories { mavenCentral() }

    ext {
        mybatisPlusVersion = "3.5.15"
        hutoolVersion = "5.8.43"
        jwtVersion = "0.13.0"
        aliyunOssVersion = "3.18.5"
        jaudiotaggerVersion = "3.0.1"
        springDocVersion = "3.0.0"
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "io.spring.dependency-management"

    //导入 Spring Boot BOM管理依赖版本
    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom "com.baomidou:mybatis-plus-bom:${mybatisPlusVersion}"
        }
    }

    java {
        toolchain { languageVersion = JavaLanguageVersion.of(17) }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.compilerArgs.add("-parameters") // 强制编译器保留原始参数名，SpEL就能正确解析自定义参数名
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        // 使用 JUnit 5
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    //将FatJar输出目录到根项目 build/libs
    tasks.withType(Jar).configureEach {
        destinationDirectory = rootProject.layout.buildDirectory.dir("libs")
    }

    //执行bootRun任务时从根目录读取配置
    tasks.withType(JavaExec).tap {
        configureEach {
            workingDir = rootProject.projectDir
        }
    }

}